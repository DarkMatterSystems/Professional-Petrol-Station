
DefineDomainInclude:0101_Entity_Delivery

    | Copyright (C) 2017 Dark Matter Systems Ltd. All rights reserved.

    | MASL code within comment sections are UK Crown Copyright.

    Entity:Delivery
        Attribute
            Time_Hour   : Timestamp_hours
            Time_Minute : Timestamp_minutes
            Time_Second : Timestamp_seconds
            Volume      : Volume_delivered
            Money       : Cost
        Relationship
            Being_made_using --> Pump
        Identifier:Delivery_Identifier
            ForeignKey
                Being_made_using : Pump : Pump_Identifier
        State

            Dataset:Pump_Number
                Equipment_Number : Pump_number
                Time_Hour        : Timestamp_hours
                Time_Minute      : Timestamp_minutes
                Time_Second      : Timestamp_seconds

            |-----------

            CreationDataState:Creating_Delivery:Pump_Number
                StateAction
                    |
                    | //# Create a delivery instance and enable the pump.
                    |
                    | new_delivery := create DELIVERY (
                    |   Pump_Number => Pump_Id,
                    |   Time => timestamp'now,
                    |   Volume_Delivered => 0.0,
                    |   Cost => 0.0,
                    |       Current_State => Creating_Delivery);
                    |
                    Assignment
                        SetInteger
                            Delivery.Timestamp_hours   := Timestamp_hours
                            Delivery.Timestamp_minutes := Timestamp_minutes
                            Delivery.Timestamp_seconds := Timestamp_seconds
                        SetFixed
                            Delivery.Volume_delivered := <<FixedZero>>
                            Delivery.Cost             := <<FixedZero>>
                    |
                    | delivering_pump := find_one PUMP (Pump_Number = Pump_Id);
                    |
                    | link new_delivery R3 delivering_pump;
                    |
                    | generate PUMP.Pump_Enabled() to delivering_pump;
                    |
                    IdentifierSelection:Pump:Pump_Identifier
                        IdentifierKey
                            Pump : Pump_number : Pump_number
                        KeySelection
                            Relate
                                Delivery : Being_made_using : Pump
                                Pump     : Being_used_for   : Delivery
                            GenerateEvent
                                Pump : Pump_enabled
                Transition
                    EventState
                        Fuel_unit_delivered -> Calculating_Price_Of_Unconstrained_Delivery
                        Delivery_complete   -> Delivery_Cancelled
                    IgnoreEvent
                        Delete_delivery
                    CannotHappen
                        Create_delivery

            |-----------

            DynamicState:Calculating_Price_Of_Unconstrained_Delivery
                StateAction
                    |
                    | //# A unit of petrol has been delivered.
                    | //# Update the volume delivered and current cost.
                    |
                    | new_volume := this.Volume_Delivered + 0.01;
                    | this.Volume_Delivered := new_volume;
                    |
                    | grade := this->R3->R1->R2;
                    | new_cost := this.Cost + grade.Unit_Price;
                    | this.Cost := new_cost;
                    |
                    Traverse
                        Navigation
                            Delivery : Being_made_using  : Pump
                            Pump     : Pumping_fuel_from : Tank
                            Tank     : Stores            : Fuel_Grade
                        TraverseSelection
                            Assignment
                                Evaluation:Delivery.Volume_delivered
                                    Addition
                                        AdditionData
                                            Delivery.Volume_delivered
                                            Fuel_Grade.Unit_volume
                                Evaluation:Delivery.Cost
                                    Addition
                                        AdditionData
                                            Delivery.Cost
                                            Fuel_Grade.Unit_price
                Transition
                    EventState
                        Fuel_unit_delivered -> Calculating_Price_Of_Unconstrained_Delivery
                        Delivery_complete   -> Delivery_Complete_State
                    IgnoreEvent
                        Delete_delivery
                    CannotHappen
                        Create_delivery

            |-----------

            DynamicState:Delivery_Complete_State
                StateAction
                    |
                    | //# Delivery completed so create a transaction.
                    | //# Inform the tank amount of fuel dispensed.
                    |
                    | generate TRANSACTION.Create_Transaction(this.Time, this.Pump_Number, this.Cost);
                    |
                    | supplying_tank := this->R3->R1;
                    | generate TANK.Fuel_Used(this.Volume_Delivered) to supplying_tank;
                    |
                    | pump_instance := this->R3;
                    | unlink this R3 pump_instance;
                    | delete this;
                    |
                    Traverse
                        Navigation
                            Delivery : Being_made_using  : Pump
                            Pump     : Pumping_fuel_from : Tank
                        TraverseSelection
                            GenerateDataEvent:Transaction:Create_transaction
                                Delivery_pump         : Pump.Pump_number
                                Delivery_cost         : Delivery.Cost
                                Delivery_time_hours   : Delivery.Timestamp_hours
                                Delivery_time_minutes : Delivery.Timestamp_minutes
                                Delivery_time_seconds : Delivery.Timestamp_seconds
                            GenerateDataEvent:Tank:Fuel_used
                                Delivered_volume : Delivery.Volume_delivered
                    GenerateEvent
                        Delivery : Delete_delivery
                Transition
                    EventState
                        Delete_delivery -> Delivery_Vanishes
                    CannotHappen
                        Create_delivery
                        Fuel_unit_delivered
                        Delivery_complete

            |-----------

            DynamicState:Delivery_Cancelled
                StateAction
                    |
                    | //# The customer has not pumped any fuel therefore there is
                    | //# no pending transaction to be paid.
                    |
                    | ATTENDANT~>Delivery_Cancelled();
                    |
                    | pump_instance := this->R3;
                    | unlink this R3 pump_instance;
                    | delete this;
                    |
                    Template
                        <<:>>ATTENDANT~>Delivery_Cancelled
                        <<:>>
                    GenerateEvent
                        Delivery : Delete_delivery
                Transition
                    EventState
                        Delete_delivery -> Delivery_Vanishes
                    CannotHappen
                        Create_delivery
                        Fuel_unit_delivered
                        Delivery_complete

            |-----------

            DeletionState:Delivery_Vanishes
                StateAction
                    Traverse
                        Navigation
                            Delivery : Being_made_using : Pump
                        TraverseSelection
                            Unrelate
                                Delivery : Being_made_using : Pump
                Transition
                    EventState
                        Create_delivery -> Creating_Delivery
                    CannotHappen
                        Fuel_unit_delivered
                        Delivery_complete
                        Delete_delivery
